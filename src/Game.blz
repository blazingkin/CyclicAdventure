require GameState
require Player
require Class
import GameUtil

:main
    print("Welcome to cyclic adventure!!")
    print("You can quit at any time by typing -1")
    print_seperator()
    gs = setup_game()
    while loop!(gs)
    end
end

:loop(game_state)
    prompt(game_state)
    input = number_input()
    if input == -1
        return false
    end
    process_input!(game_state, input)
    if game_state.player.get_health() <= 0
        print("You have died! Try again!")
        return false
    end
    if game_state.location == game_state.done
        return false
    end
    return true
end

:prompt(game_state)   
    if game_state.location == game_state.overworld
        game_state.describe_level()
        game_state.print_options()
    else if game_state.location == game_state.combat
        game_state.describe_combat()
        game_state.player.actions().each(x, i -> print(i+". "+(x[0])))
    end
end

# This is the first state of the game.
:setup_game()

    # Prompt the player for characteristics
    print("What is your name?")
    name = string_input()
    print_seperator()
    valid = false
    while valid != true
        print("What is your class?")
        classes().each(x, i -> print(i+". "+x))
        class_number = number_input()
        valid = class_number < classes().size()
    end
    print_seperator()
    player = Player(name, class_name(class_number))
    print("Welcome "+player)
    inform("Press enter to continue")

    print_seperator()
    print("How many rooms do you want to play with? 8 is small. 12 is average. 17 is large")
    valid = false
    while valid != true
        num_levels = number_input()
        valid = num_levels > 0
    end
    print_seperator()
    GameState(player, num_levels)
end

:process_input(game_state, input)
    if game_state.location == game_state.overworld
        if input >= game_state.current_level().number_of_options() || input < 0
            print("Please enter a valid option")
            return ""
        end

        game_state.current_level().execute_option(input, game_state)
        print_seperator()
    else if game_state.location == game_state.combat
        
        if game_state.get_combat_state().over?()
            game_state.end_combat()
            return ""
        end

        if input >= game_state.player.actions().size() || input < 0
            print("Please enter a valid option")
            return ""
        end

        action = game_state.player.actions()
        action = action[input]
        action = action[1]

        if action!(game_state.get_combat_state()) == false
            return ""
        end

        ## End players turn 
        game_state.get_combat_state().next()
        if game_state.get_combat_state().over?()
            game_state.end_combat()
            return ""
        end

        ## Take enemies turn
        game_state.get_combat_state().enemies_take_turn()
        game_state.get_combat_state().next()
        if game_state.get_combat_state().over?()
            game_state.end_combat()
            return ""
        end


    end
end